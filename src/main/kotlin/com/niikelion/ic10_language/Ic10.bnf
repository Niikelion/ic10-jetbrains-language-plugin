{
    parserClass="com.niikelion.ic10_language.parser.Ic10Parser"
    extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

    psiClassPrefix="Ic10"
    psiImplClassSuffix="Impl"
    psiPackage="com.niikelion.ic10_language.psi"
    psiImplPackage="com.niikelion.ic10_language.psi.impl"

    elementTypeHolderClass="com.niikelion.ic10_language.psi.Ic10Types"
    elementTypeClass="com.niikelion.ic10_language.psi.Ic10ElementType"
    tokenTypeClass="com.niikelion.ic10_language.psi.Ic10TokenType"
}

ic10File ::= (line (CRLF line)*)?

line ::= (label | operation)? COMMENT?

label ::= labelName COLON {
    mixin="com.niikelion.ic10_language.psi.impl.Ic10NamedElementImpl"
    implements="com.niikelion.ic10_language.psi.Ic10NamedElement"
}

operation ::= operationName value*

value ::= channel | hash | number | constant | referenceName
constant ::= constantName DOT constantName
channel ::= referenceName COLON channelNumber
channelNumber ::= DECIMAL
number ::= BINARY | DECIMAL | HEXADECIMAL | FLOAT
hash ::= OPENBRACKET hashValue CLOSEBRACKET
hashValue ::= HASHCONTENT
referenceName ::= NAME {
    mixin="com.niikelion.ic10_language.psi.impl.Ic10NamedElementImpl"
    implements="com.niikelion.ic10_language.psi.Ic10NamedElement"
}
labelName ::= NAME
operationName ::= NAME
constantName ::= NAME